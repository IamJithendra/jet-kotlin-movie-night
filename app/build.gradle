plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk rootProject.compile_sdk_version
    buildToolsVersion rootProject.build_tools_version

    defaultConfig {
        applicationId "com.ang.acb.movienight"
        minSdk rootProject.min_sdk_version
        targetSdk rootProject.target_sdk_version
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'TMDB_API_KEY', TMDB_API_KEY
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = jvm_version
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion koltin_version
    }
}

kapt {
    correctErrorTypes true
}

// Dependency versions are defined in the top level build.gradle file.
dependencies {
    // Kotlin Couroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$koltin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$koltin_coroutines_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // AndroidX
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.annotation:annotation:$androidx_annotations_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    implementation "androidx.navigation:navigation-compose:$navigation_compose_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_navigation_compose_version"
    implementation "androidx.paging:paging-compose:$paging_compose_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$viewmodel_compose_version"

    // Google
    implementation "com.google.android.material:material:$material_version"
    implementation "com.google.accompanist:accompanist-glide:$accompanist_glide_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_flowlayout_version"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_insets_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_systemui_version"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_logging_version"

    // Utils
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenbp_version"
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Testing
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptTest "com.google.dagger:hilt-compiler:$hilt_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_version"
    androidTestImplementation "androidx.test.ext:junit: $junit_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core: $espresso_core_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}